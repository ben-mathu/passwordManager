plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.dagger.hilt)
    id 'maven-publish'
}

def coreVersion = "1.0.0"

android {
    namespace "com.benatt.core"
    compileSdk 35

    defaultConfig {
        minSdk 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles("consumer-rules.pro")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {

    implementation(libs.androidx.core)
    implementation(libs.androidx.appcompat)
    implementation(libs.google.material)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.junit)
    androidTestImplementation(libs.androidx.test.espresso)

    // Billing
    api(libs.billing)
    api(libs.guava)

    // Dagger
    implementation(libs.hilt.android)
    annotationProcessor(libs.hilt.compiler)
    implementation(libs.androidx.navigation.fragment)
    implementation(libs.androidx.navigation.ui)

    // Preferences
    api(libs.preferences)
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.benatt'
                artifactId = 'core'
                version = coreVersion

                artifact(bundleReleaseAar)
            }
        }

        repositories {
            mavenLocal()

            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ben-mathu/passwordManager") // Replace with your username and repository name
                credentials {
                    username = localProperties["GPR_USER"]
                    password = localProperties["GPR_KEY"]
                }
            }
        }
    }
}